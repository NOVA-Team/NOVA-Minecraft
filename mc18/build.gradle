buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
	}
}

apply plugin: "maven-publish"
apply plugin: "com.jfrog.artifactory"
apply from: "https://raw.githubusercontent.com/NOVA-Team/NOVA-Gradle/master/shared-scripts/java.gradle"

publishing {
	publications {
		main(MavenPublication) {
			from components.java

			artifactId "NovaMinecraft-Wrapper-MC1.8"

			artifact sourcesJar
			artifact javadocJar

			pom.withXml(writePom(project.properties))
		}
	}
}

artifactory {
	publish {
		defaults {
			publications("main")
			publishPom = true
		}
	}
}

artifacts {
	archives jar
}

apply plugin: "forge"

minecraft {
	version = property("forge.version")
	mappings = "stable_nodoc_16"
	runDir = "run"
}

dependencies {
	compile rootProject
	compile group: "nova.core", name: "NovaCore", version: property("nova.version"), changing: true
	compile "nova.wrapper.mc18:NovaWrapper-MC1.8:0.1-SNAPSHOT:deobf"
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"

		// replace version and mcversion
		expand "version": project.version, "mcversion": project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}
