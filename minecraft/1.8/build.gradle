apply plugin: "maven-publish"
apply plugin: "com.jfrog.artifactory"
apply from: "https://raw.githubusercontent.com/NOVA-Team/NOVA-Gradle/master/shared-scripts/java.gradle"

idea.module.name = "Minecraft-MC-1.8"
archivesBaseName = "NOVA-Minecraft-Wrapper-MC1.8"

publishing {
	publications {
		main(MavenPublication) {
			from components.java

			artifactId archivesBaseName

			artifact sourcesJar
			artifact javadocJar

			pom.withXml(writePom(project.properties))
		}
	}
}

artifactory {
	publish {
		defaults {
			publications("main")
			publishPom = true
		}
	}
}

artifacts {
	archives jar
}

apply plugin: "forge"

minecraft {
	version = property("minecraft.version") + "-" + property("forge.version")
	mappings = "stable_nodoc_16"
	runDir = "run"
}

dependencies {
	compile rootProject
	compile "nova.core:NOVA-Core:${property("nova.version")}"
	compile "nova.core:NOVA-Core-Wrapper-MC1.8:${property("nova.version")}:deobf"
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"

		// replace version and mcversion
		expand "version": project.version, "mcversion": project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}
